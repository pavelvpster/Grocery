plugins {
    id 'org.springframework.boot' version '2.7.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'pmd'
    id "com.github.spotbugs" version '5.0.8'
    id 'jacoco'
    id 'org.liquibase.gradle' version '2.1.1'
}

group 'org.interactiverobotics'
version '1.2-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11

application {
    mainClass = 'org.interactiverobotics.grocery.GroceryApplication'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jetty'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    implementation 'org.postgresql:postgresql:42.4.0'

    implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
    implementation group: 'org.hibernate', name: 'hibernate-validator', version: '7.0.4.Final'

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation group: 'io.springfox', name: 'springfox-swagger2', version: '3.0.0'
    implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: '3.0.0'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf'
    implementation group: 'nz.net.ultraq.thymeleaf', name: 'thymeleaf-layout-dialect', version: '3.1.0'

    liquibaseRuntime 'org.liquibase:liquibase-core:4.11.0'
    liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:3.0.2'
    liquibaseRuntime 'info.picocli:picocli:4.6.3'
    liquibaseRuntime 'org.postgresql:postgresql:42.4.0'

    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
}

checkstyle {
    configFile = file('codechecks/checkstyle.xml')
}

pmd {
    consoleOutput = true
    ruleSetFiles = files("codechecks/ruleset.xml")
}

test {
    useJUnit()
    exclude '**/*IntegrationTest.*'
    finalizedBy jacocoTestReport
}

task integrationTest(type: Test) {
    useJUnit()
    include '**/*IntegrationTest.*'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
    }
}

liquibase {
    activities {
        main {
            changeLogFile 'src/main/resources/db/db.changelog.xml'
            url 'jdbc:postgresql://localhost/grocery'
            username 'grocery'
            password 'grocery'
        }
    }
}

wrapper {
    gradleVersion = '7.4'
}
