buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.4.RELEASE")
        classpath("org.liquibase:liquibase-gradle-plugin:2.0.1")
    }
}

group 'org.interactiverobotics'
version '1.1-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'org.liquibase.gradle'

sourceCompatibility = 1.8

mainClassName = 'org.interactiverobotics.grocery.GroceryApplication'

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile("org.springframework.boot:spring-boot-starter-jetty")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile 'org.postgresql:postgresql:42.2.4.jre7'
    compile group: 'javax.el', name: 'javax.el-api', version: '3.0.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8'
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2'
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.9.2'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf'
    compile group: 'nz.net.ultraq.thymeleaf', name: 'thymeleaf-layout-dialect', version: '2.3.0'

    compile group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1'

    liquibaseRuntime 'org.liquibase:liquibase-core:3.6.1'
    liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:2.0.1'
    liquibaseRuntime 'org.postgresql:postgresql:42.2.4.jre7'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
}

checkstyle {
    configFile = new File(rootDir, '/codechecks/checkstyle.xml')
    toolVersion = '7.1.1'
}

pmd {
    ruleSetFiles = files(new File(rootDir, '/codechecks/ruleset.xml'))
}

findbugs {
    ignoreFailures = false
    effort = "max"
    includeFilter = new File(rootDir, "/codechecks/findbugs-security-include.xml")
    excludeFilter = new File(rootDir, "/codechecks/findbugs-security-exclude.xml")
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

test {
    exclude '**/*IntegrationTest.*'
}

task integrationTest(type: Test) {
    include '**/*IntegrationTest.*'
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/reports/coverage"
    }
}

build.dependsOn jacocoTestReport

liquibase {
    activities {
        main {
            changeLogFile 'src/main/resources/db/db.changelog.xml'
            url 'jdbc:postgresql://localhost/grocery'
            username 'grocery'
            password 'grocery'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.9'
}
